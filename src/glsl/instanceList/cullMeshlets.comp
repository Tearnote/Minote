#version 460
#pragma shader_stage(compute)

layout(local_size_x = 64) in;

#include "../types.glsl"
#include "../util.glsl"

layout(binding = 0) uniform CullingData {
	float4x4 u_view;
	float4 u_frustum;
	float u_P00;
	float u_P11;
};
layout(binding = 1, std430) restrict readonly buffer Meshlets {
	Meshlet b_meshlets[];
};
layout(binding = 2, std430) restrict readonly buffer Instances {
	Instance b_instances[];
};
layout(binding = 3, std430) restrict readonly buffer Transforms {
	float3x3x4 b_transforms[];
};
layout(binding = 4) uniform sampler2D s_hiz;
layout(binding = 5, std430) restrict buffer OutInstanceCount {
	uint4 b_outInstanceCount;
};
layout(binding = 6, std430) restrict writeonly buffer OutInstances {
	Instance b_outInstances[];
};
layout(binding = 7, std430) restrict buffer GroupCounter {
	uint b_groupCounter;
};

layout(push_constant) uniform Constants {
	uint u_instanceCount;
};

layout(constant_id = 0) const uint MaxTrisPerMeshlet = 0;
layout(constant_id = 1) const float ZNear = 0.0;
layout(constant_id = 2) const uint HiZSizePacked = 0;
const uint2 HiZSize = uint2(U16FROMU32(HiZSizePacked));
layout(constant_id = 3) const uint HiZInnerPacked = 0;
const uint2 HiZInner = uint2(U16FROMU32(HiZInnerPacked));

// 2D Polyhedral Bounds of a Clipped, Perspective-Projected 3D Sphere. Michael Mara, Morgan McGuire. 2013
bool projectSphere(float3 C, float r, float znear, float P00, float P11, out float4 aabb) {
	
	if (C.z < r + znear)
		return false;
	
	float2 cx = -C.xz;
	float2 vx = float2(sqrt(dot(cx, cx) - r * r), r);
	float2 minx = mat2(vx.x, vx.y, -vx.y, vx.x) * cx;
	float2 maxx = mat2(vx.x, -vx.y, vx.y, vx.x) * cx;
	
	float2 cy = -C.yz;
	float2 vy = float2(sqrt(dot(cy, cy) - r * r), r);
	float2 miny = mat2(vy.x, vy.y, -vy.y, vy.x) * cy;
	float2 maxy = mat2(vy.x, -vy.y, vy.y, vy.x) * cy;
	
	aabb = float4(minx.x / minx.y * P00, miny.x / miny.y * P11, maxx.x / maxx.y * P00, maxy.x / maxy.y * P11);
	aabb = aabb.xwzy * float4(0.5f, -0.5f, 0.5f, -0.5f) + float4(0.5f); // clip space -> uv space
	
	return true;
	
}

void main() {
	
	uint gid = gl_GlobalInvocationID.x;
	uint lid = gl_LocalInvocationID.x;
	if (gid < u_instanceCount) {
		
		// Retrieve meshlet data
		
		Instance instance = b_instances[gid];
		Meshlet meshlet = b_meshlets[instance.meshletIdx];
		uint transformIdx = instance.objectIdx;
		float4x4 transform = getTransform(b_transforms[transformIdx]);
		
		// Transform meshlet data
		
		float scale = max(
			length(transform[0].xyz), max(
			length(transform[1].xyz),
			length(transform[2].xyz)));
		float3 boundingSphereCenter = float3(transform * float4(meshlet.boundingSphereCenter, 1.0));
		float boundingSphereRadius = meshlet.boundingSphereRadius * scale;
		
		bool visible = true;
		
		// Frustum culling
		
		float3 viewCenter = float3(u_view * float4(boundingSphereCenter, 1.0));
		visible = visible && (
			(viewCenter.z * u_frustum[1] - abs(viewCenter.x) * u_frustum[0] > -boundingSphereRadius) &&
			(viewCenter.z * u_frustum[3] - abs(viewCenter.y) * u_frustum[2] > -boundingSphereRadius));
		
		// Occlusion culling
		
		if (visible) { // Avoid a texture lookup if not needed
			
			viewCenter.y *= -1;
			
			float4 aabb;
			if (projectSphere(viewCenter, boundingSphereRadius, ZNear, u_P00, u_P11, aabb))
			{
				float width = (aabb.z - aabb.x) * HiZInner.x;
				float height = (aabb.w - aabb.y) * HiZInner.y;
				
				float level = floor(log2(max(width, height)));
				float2 offset = float2(HiZSize - HiZInner) / 2.0 / float2(HiZSize);
				float2 scale = float2(HiZInner) / float2(HiZSize);
				
				// Sampler is set up to do min reduction, so this computes the minimum depth of a 2x2 texel quad
				float2 depthUv = saturate((aabb.xy + aabb.zw) * 0.5) * scale + offset;
				float depth = textureLod(s_hiz, depthUv, level).x;
				float depthSphere = ZNear / (viewCenter.z - boundingSphereRadius);
				
				visible = (depthSphere > depth);
			}
			
		}
		
		// Write the instance
		
		if (visible) {
			
			uint outIdx = atomicAdd(b_outInstanceCount.w, 1);
			b_outInstances[outIdx] = instance;
			
		}
		
	}
	
	// Update group counter
	
	barrier();
	
	if (lid == gl_WorkGroupSize.x - 1) {
		
		uint groupCount = atomicAdd(b_groupCounter, 1);
		if (groupCount == gl_NumWorkGroups.x - 1) {
			
			// We're the last workgroup, write the instance groups
			
			groupMemoryBarrier();
			b_outInstanceCount.x = divRoundUp(b_outInstanceCount.w * MaxTrisPerMeshlet, 4 * 256);
			
		}
		
	}
	
}
