#version 460
#pragma shader_stage(compute)

layout(local_size_x = 16, local_size_y = 16) in;

#include "util.glsl"

layout(binding = 0) uniform sampler2D s_source;
layout(binding = 1) restrict writeonly uniform image2D i_target;

shared vec4 sh_outer[gl_WorkGroupSize.x + 2][gl_WorkGroupSize.y + 2];
shared vec4 sh_inner[gl_WorkGroupSize.x + 1][gl_WorkGroupSize.y + 1];

// https://github.com/keijiro/KinoBloom/blob/master/Assets/Kino/Bloom/Shader/Bloom.cginc
vec4 karisAverage(vec4 _s1, vec4 _s2, vec4 _s3, vec4 _s4) {
	
	float s1w = 1.0 / (luma(_s1.rgb) + 1.0);
	float s2w = 1.0 / (luma(_s2.rgb) + 1.0);
	float s3w = 1.0 / (luma(_s3.rgb) + 1.0);
	float s4w = 1.0 / (luma(_s4.rgb) + 1.0);
	float one_div_wsum = 1.0 / (s1w + s2w + s3w + s4w);
	
	return (_s1 * s1w + _s2 * s2w + _s3 * s3w + _s4 * s4w) * one_div_wsum;
	
}

void main() {
	
	const uvec2 gid = gl_GlobalInvocationID.xy;
	const uvec2 lid = gl_LocalInvocationID.xy;
	const ivec2 sourceSize = textureSize(s_source, 0);
	const ivec2 targetSize = imageSize(i_target);
	
	vec2 uv = (vec2(gid) + vec2(0.5)) / vec2(targetSize);
	vec2 texel = vec2(1.0) / vec2(sourceSize);
	vec2 texel2 = texel * 2.0;
	
	if (lid.x <= 1)
		sh_outer[lid.x  ][lid.y+2] = textureLod(s_source, uv + vec2(-texel2.x, texel2.y), 0.0);
	if (lid.y <= 1)
		sh_outer[lid.x+2][lid.y  ] = textureLod(s_source, uv + vec2(texel2.x, -texel2.y), 0.0);
	if (lid.x <= 1 && lid.y <= 1)
		sh_outer[lid.x  ][lid.y  ] = textureLod(s_source, uv - texel2, 0.0);
	
	sh_outer[lid.x+2][lid.y+2] = textureLod(s_source, uv + texel2, 0.0);
	
	if (lid.x == 0)
		sh_inner[lid.x  ][lid.y+1] = textureLod(s_source, uv + vec2(-texel.x, texel.y), 0.0);
	if (lid.y == 0)
		sh_inner[lid.x+1][lid.y  ] = textureLod(s_source, uv + vec2(texel.x, -texel.y), 0.0);
	if (lid.x == 0 && lid.y == 0)
		sh_inner[lid.x  ][lid.y  ] = textureLod(s_source, uv - texel, 0.0);
	
	sh_inner[lid.x+1][lid.y+1] = textureLod(s_source, uv + texel, 0.0);
	
	memoryBarrierShared();
	barrier();
	
	if (any(greaterThanEqual(gid, targetSize)))
		return;
	
	vec4 resultInner = karisAverage(
		sh_inner[lid.x  ][lid.y  ],
		sh_inner[lid.x+1][lid.y  ],
		sh_inner[lid.x  ][lid.y+1],
		sh_inner[lid.x+1][lid.y+1]);
	
	vec4 resultOuter1 = karisAverage(
		sh_outer[lid.x  ][lid.y  ],
		sh_outer[lid.x+1][lid.y  ],
		sh_outer[lid.x  ][lid.y+1],
		sh_outer[lid.x+1][lid.y+1]);
	vec4 resultOuter2 = karisAverage(
		sh_outer[lid.x+1][lid.y  ],
		sh_outer[lid.x+2][lid.y  ],
		sh_outer[lid.x+1][lid.y+1],
		sh_outer[lid.x+2][lid.y+1]);
	vec4 resultOuter3 = karisAverage(
		sh_outer[lid.x  ][lid.y+1],
		sh_outer[lid.x+1][lid.y+1],
		sh_outer[lid.x  ][lid.y+2],
		sh_outer[lid.x+1][lid.y+2]);
	vec4 resultOuter4 = karisAverage(
		sh_outer[lid.x+1][lid.y+1],
		sh_outer[lid.x+2][lid.y+1],
		sh_outer[lid.x+1][lid.y+2],
		sh_outer[lid.x+2][lid.y+2]);
	
	vec4 result =
		resultInner  * 0.5   +
		resultOuter1 * 0.125 +
		resultOuter2 * 0.125 +
		resultOuter3 * 0.125 +
		resultOuter4 * 0.125;
	imageStore(i_target, ivec2(gid), result);
	
}
