#version 460
#pragma shader_stage(compute)

layout(local_size_x = 8, local_size_y = 8) in;

#include "skyTypes.glsl"
#include "../types.glsl"

#define U_ATMO u_atmo
#define S_TRANSMITTANCE s_transmittance

layout(binding = 0) uniform WorldConstants {
	World u_world;
};
layout(binding = 1) uniform Atmosphere {
	AtmosphereParams U_ATMO;
};
layout(binding = 2) uniform sampler2D S_TRANSMITTANCE;
layout(binding = 3) uniform sampler2D s_skyView;
layout(binding = 4) uniform usampler2D s_visbuf;
layout(binding = 5) restrict writeonly uniform image2D i_target;

layout(push_constant) uniform Constants {
	uvec2 u_skyViewSize;
	uvec2 u_targetSize;
}; 

#include "sky.glsl"

void main() {
	
	// Bounds check
	
	uvec2 gid = gl_GlobalInvocationID.xy;
	if (any(greaterThanEqual(gid, u_targetSize)))
		return;
	
	// Retrieve visibility sample
	
	uint visSample = texelFetch(s_visbuf, ivec2(gid), 0).x;
	if (visSample != -1u) // Sky sample
		return;
	
	vec2 gUv = vec2(gid) / vec2(u_targetSize);
	vec3 clipSpace = vec3(gUv * vec2(2.0) - vec2(1.0), 0.0);
	vec4 hPos = u_world.viewProjectionInverse * vec4(clipSpace, 1.0);

	vec3 worldDir = normalize(hPos.xyz);
	vec3 worldPos = u_world.cameraPos + vec3(0.0, 0.0, u_atmo.bottomRadius);

	float viewHeight = length(worldPos);

	vec2 uv;
	vec3 upVector = normalize(worldPos);
	float viewZenithCosAngle = dot(worldDir, upVector);

	vec3 sideVector = normalize(cross(upVector, worldDir)); // assumes non parallel vectors
	vec3 forwardVector = normalize(cross(sideVector, upVector)); // aligns toward the sun light but perpendicular to up vector
	vec2 lightOnPlane = vec2(dot(u_world.sunDirection, forwardVector), dot(u_world.sunDirection, sideVector));
	lightOnPlane = normalize(lightOnPlane);
	float lightViewCosAngle = lightOnPlane.x;

	bool intersectGround = (raySphereIntersectNearest(worldPos, worldDir, vec3(0.0), u_atmo.bottomRadius) >= 0.0);

	skyViewLutParamsToUv(intersectGround, viewZenithCosAngle, lightViewCosAngle, u_skyViewSize, viewHeight, uv, u_atmo.bottomRadius);
	vec3 skyView = textureLod(s_skyView, uv, 0.0).rgb;
	vec3 sun = getSunLuminance(worldPos, worldDir, u_world.sunDirection, u_world.sunIlluminance)
		* (120000.0 / u_world.sunIlluminance);
	
	imageStore(i_target, ivec2(gid), vec4(skyView + sun, 1.0));
	
}
