#version 460
#pragma shader_stage(compute)

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

#include "skyTypes.glsl"
#include "../types.glsl"

#define U_ATMO u_atmo
#define S_TRANSMITTANCE s_transmittance
#define S_MULTISCATTERING s_multiscattering

layout(binding = 0) uniform WorldConstants {
	World u_world;
};
layout(binding = 1) uniform Atmosphere {
	AtmosphereParams U_ATMO;
};
layout(binding = 2) uniform sampler2D S_TRANSMITTANCE;
layout(binding = 3) uniform sampler2D S_MULTISCATTERING;
layout(binding = 4) restrict writeonly uniform image3D i_aerialPerspective;

#include "sky.glsl"

void main() {
	
	const uvec3 gid = gl_GlobalInvocationID;
	const ivec3 size = imageSize(i_aerialPerspective);

	if (any(greaterThanEqual(gid, size)))
		return;

	vec2 pixPos = vec2(gid) + vec2(0.5);
	vec3 clipSpace = vec3((pixPos / vec2(size.xy)) * vec2(2.0) - vec2(1.0), 0.0);
	vec4 hPos = u_world.viewProjectionInverse * vec4(clipSpace, 1.0);
	vec3 worldDir = normalize(hPos.xyz);
	float earthR = u_atmo.bottomRadius;
	vec3 earthO = vec3(0.0, 0.0, -earthR);
	vec3 camPos = u_world.cameraPos + vec3(0.0, 0.0, earthR);
	vec3 sunDir = u_world.sunDirection;
	vec3 sunLuminance = vec3(0.0);

	float slice = ((float(gid.z) + 0.5) / size.z);
	slice *= slice;	// squared distribution
	slice *= size.z;

	vec3 worldPos = camPos;
	float viewHeight;
	
	// Compute position from froxel information
	float tMax = aerialPerspectiveSliceToDepth(slice);
	vec3 newWorldPos = worldPos + tMax * worldDir;
	
	// If the voxel is under the ground, make sure to offset it out of the ground.
	viewHeight = length(newWorldPos);
	if (viewHeight <= (u_atmo.bottomRadius + PLANET_RADIUS_OFFSET)) {
		
		// Apply a position offset to make sure no artefact are visible close to the earth boundaries for large voxel.
		newWorldPos = normalize(newWorldPos) * (u_atmo.bottomRadius + PLANET_RADIUS_OFFSET + 0.001);
		worldDir = normalize(newWorldPos - camPos);
		tMax = length(newWorldPos - camPos);
		
	}
	float tMaxMax = tMax;
	
	// Move ray marching start up to top atmosphere.
	viewHeight = length(worldPos);
	if (viewHeight >= u_atmo.topRadius) {
		
		vec3 prevWorldPos = worldPos;
		if (!moveToTopAtmosphere(worldPos, worldDir, u_atmo.topRadius)) {
			
			// Ray is not intersecting the atmosphere
			imageStore(i_aerialPerspective, ivec3(gid), vec4(0.0, 0.0, 0.0, 1.0));
			return;
			
		}
		float lengthToAtmosphere = length(prevWorldPos - worldPos);
		if (tMaxMax < lengthToAtmosphere) {
			
			// tMaxMax for this voxel is not within earth atmosphere
			imageStore(i_aerialPerspective, ivec3(gid), vec4(0.0, 0.0, 0.0, 1.0));
			return;
			
		}
		// Now world position has been moved to the atmosphere boundary: we need to reduce tMaxMax accordingly.
		tMaxMax = max(0.0, tMaxMax - lengthToAtmosphere);
	}
	
	const bool ground = false;
	const float sampleCountIni = max(1.0, float(gid.z + 1.0) * 2.0);
	const bool variableSampleCount = false;
	const bool mieRayPhase = true;
	SingleScatteringResult ss = integrateScatteredLuminance(worldPos, worldDir, sunDir,
		ground, sampleCountIni, variableSampleCount, mieRayPhase, tMaxMax, u_world.sunIlluminance);
	
	const float transmittance = dot(ss.transmittance, vec3(1.0 / 3.0, 1.0 / 3.0, 1.0 / 3.0));
	imageStore(i_aerialPerspective, ivec3(gid), vec4(ss.L, 1.0 - transmittance));
	
}
