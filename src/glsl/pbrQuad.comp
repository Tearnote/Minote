#version 460
#pragma shader_stage(compute)

layout(local_size_x = 1, local_size_y = 64) in;

#include "sky/skyAccess.glsl"
#include "visibilityTypes.glsl"
#include "visibility.glsl"
#include "constants.glsl"
#include "types.glsl"
#include "quad.glsl"
#include "util.glsl"

#define B_INDICES b_indices
#define B_VERTICES b_vertices
#define B_NORMALS b_normals
#define B_COLORS b_vtxColors
#define B_LISTS b_lists

layout(binding = 0) uniform WorldConstants {
	World u_world;
};

layout (binding = 1, std430) restrict readonly buffer Descriptors {
	MeshDescriptor b_descriptors[];
};
layout (binding = 2, std430) restrict readonly buffer Instances {
	Instance b_instances[];
};
layout (binding = 3, std430) restrict readonly buffer Colors {
	vec4 b_colors[];
};
layout(binding = 4, std430) restrict readonly buffer Transforms {
	mat3x4 b_transforms[];
};

layout (binding = 5, std430) restrict readonly buffer Indices {
	uint B_INDICES[];
};
layout (binding = 6, std430) restrict readonly buffer Vertices {
	float B_VERTICES[];
};
layout (binding = 7, std430) restrict readonly buffer Normals {
	float B_NORMALS[];
};
layout (binding = 8, std430) restrict readonly buffer VtxColors {
	uint B_COLORS[];
};
layout (binding = 9, std430) restrict readonly buffer Materials {
	Material b_materials[];
};

layout(binding = 10) uniform SunLuminance {
	vec3 u_sunLuminance;
};
layout(binding = 11) uniform samplerCube s_cubemap;
layout(binding = 12) uniform sampler3D s_aerialPerspective;

layout(binding = 13) uniform usampler2D s_quadbuf;
layout(binding = 14) restrict writeonly uniform image2D i_target;
layout(binding = 15, std430) restrict readonly buffer TileLists {
	uint B_LISTS[];
};

layout(push_constant) uniform Constants {
	uvec3 u_aerialPerspectiveSize;
	uint u_tileOffset;
	uvec2 u_targetSize;
};

#include "typesAccess.glsl"
#include "worklist.glsl"

#define USE_ACCURATE_NORMAL_INTERPOLATION 0
#define IBL_MIP_COUNT 8.0

void getInstanceVertexParams(out vec3 _vertex, out vec3 _normal, out vec3 _color,
	Instance _instance, uint _triangleIdx, vec2 _uv) {
	
	// Retrieve vertex indices
	
	uint meshIdx = _instance.meshIdx;
	uint indexBase = b_descriptors[meshIdx].indexOffset;
	indexBase += _triangleIdx * 3;
	uvec3 indices = fetchIndices(indexBase);
	
	// Retrieve vertex params
	
	uint transformIdx = _instance.transformIdx;
	mat4 transform = getTransform(b_transforms[transformIdx]);
	
	uint vertexBase = b_descriptors[meshIdx].vertexOffset;
	indices += uvec3(vertexBase);
	
	mat3 vertices = {
		fetchVertex(indices.x),
		fetchVertex(indices.y),
		fetchVertex(indices.z)};
	
	mat3 normals = {
		fetchNormal(indices.x),
		fetchNormal(indices.y),
		fetchNormal(indices.z)};
	
	mat3 colors = {
		fetchColor(indices.x).xyz,
		fetchColor(indices.y).xyz,
		fetchColor(indices.z).xyz};
	
	// Transform params from model to world space
	
	mat3 normTransform = mat3(inverse(transpose(transform)));
	
	for (uint i = 0; i < 3; i += 1) {
		
		vec4 transformed = transform * vec4(vertices[i], 1.0);
		vertices[i] = transformed.xyz / transformed.w;
		
		normals[i] = normalize(normTransform * normals[i]);
		
	}
	
	// Calculate world position and barycentrics
	
	vec3 clipSpace = vec3(_uv * vec2(2.0) - vec2(1.0), 0.0);
	vec3 worldDir = normalize(vec3(u_world.viewProjectionInverse * vec4(clipSpace, 1.0)));
	
	vec3 barycentrics;
	lineTriIntersection(_vertex, barycentrics, u_world.cameraPos, worldDir, vertices);
	barycentrics = clampBarycentrics(barycentrics);
	
	// Interpolate params
	
#if USE_ACCURATE_NORMAL_INTERPOLATION
	
	_normal = normalInterp(normals, barycentrics);
	
#else //USE_ACCURATE_NORMAL_INTERPOLATION
	
	_normal = normalize(
		normals[0] * barycentrics.x +
		normals[1] * barycentrics.y +
		normals[2] * barycentrics.z);
	
#endif //USE_ACCURATE_NORMAL_INTERPOLATION
	
	_color =
		colors[0] * barycentrics.x +
		colors[1] * barycentrics.y +
		colors[2] * barycentrics.z;
	
}

vec3 envBRDFApprox(vec3 _f0, float _NoV, float _roughness) {
	
	vec4 c0 = vec4(-1.0, -0.0275, -0.572, 0.022);
	vec4 c1 = vec4(1.0, 0.0425, 1.04, -0.04);
	vec4 r = _roughness * c0 + c1;
	float a004 = min(r.x * r.x, exp2(-9.28 * _NoV)) * r.x + r.y;
	vec2 ab = vec2(-1.04, 1.04) * a004 + r.zw;
	return _f0 * ab.x + ab.y;
	
}

float dApprox(float _roughness, float _RoL) {
	
	float a = _roughness * _roughness;
	float a2 = a * a;
	float rcp_a2 = 1.0 / a2;
	// 0.5 / ln(2), 0.275 / ln(2)
	float c = 0.72134752 * rcp_a2 + 0.39674113;
	return rcp_a2 * exp2(c*_RoL - c);
	
}

vec3 shadeVisSamplePBR(uint _visValue, vec2 _screenPos) {
	
	vec2 sampleUv = _screenPos / vec2(u_targetSize);
	VisSample vis = unpackVisibility(_visValue);
	
	Instance instance = b_instances[vis.instance];
	uint materialIdx = instance.materialIdx;
	Material material = b_materials[materialIdx];
	
	vec3 vertex;
	vec3 normal;
	vec3 color;
	getInstanceVertexParams(vertex, normal, color, instance, vis.triangle, sampleUv);
	
	vec4 clipVertexW = u_world.viewProjection * vec4(vertex, 1.0);
	vec3 clipVertex = clipVertexW.xyz / clipVertexW.w;
	
	// Standard vectors
	
	vec3 viewDirection = normalize(u_world.cameraPos - vertex);
	float NoV = dot(normal, viewDirection);
	
	// Empirical sun visibility above the horizon
	
	const float sunAngularSize = radians(0.2);
	const float sunsetStart = cos(radians(90.05) - sunAngularSize);
	const float sunsetEnd = cos(radians(90.05) + sunAngularSize);
	
	float sunDot = dot(vec3(0.0, 0.0, 1.0), u_world.sunDirection);
	float sunset = (sunDot - sunsetEnd) / (sunsetStart - sunsetEnd);
	sunset = clamp(sunset, 0.0, 1.0);
	sunset = (1.0 - cos(sunset * 1.57079633)) / 2.0;
	
	vec3 sunColor = u_sunLuminance * sunset;
	
	// PBR calculation
	
	color *= b_colors[instance.colorIdx].xyz;
	vec3 f0 = max(color.rgb * material.metalness, vec3(0.04));
	
	vec3 iblDiffuse = textureLod(s_cubemap, normal, IBL_MIP_COUNT - 2.0).rgb;
	vec3 sunDiffuse = sunColor * max(dot(normal, u_world.sunDirection), 0.0);
	vec3 diffuse = color.rgb * (iblDiffuse + sunDiffuse) * (1.0 - material.metalness);
	
	vec3 reflection = reflect(viewDirection, normal);
	float iblMip = max(7.0 - 0.480898 * log(2.0 / pow(material.roughness, 4.0) - 1.0), 0.0);
	vec3 iblSpecular = vec3(textureLod(s_cubemap, -reflection, iblMip));
	float SunMinRoughness = 1.0 / 16.0;
	vec3 sunSpecular = sunColor * dApprox(max(material.roughness, SunMinRoughness), dot(-reflection, u_world.sunDirection));
	vec3 specular = iblSpecular + sunSpecular;
	
	vec3 surface = mix(diffuse, specular, envBRDFApprox(f0, NoV, material.roughness));
	
	// Aerial perspective
	
	float slice = aerialPerspectiveDepthToSlice(1.0 - clipVertex.z);
	float weight = 1.0;
	if (slice < 0.5) {
		
		// We multiply by weight to fade to 0 at depth 0. That works for luminance and opacity.
		weight = clamp(slice * 2.0, 0.0, 1.0);
		slice = 0.5;
		
	}
	float w = sqrt(slice / u_aerialPerspectiveSize.z);	// squared distribution
	
	vec3 ap = weight * textureLod(s_aerialPerspective, vec3(sampleUv, w), 0.0).xyz;
	
	// Result write
	
	return surface + ap;
	
}

void main() {
	
	uvec2 lid = mortonOrder(gl_LocalInvocationID.y);
	uvec2 gid = getTileGid(gl_WorkGroupID.x) + lid;
	
	// Retrieve cluster data
	
	uvec2 quadbufSample;
	if (all(lessThan(gid, u_targetSize)))
		quadbufSample = texelFetch(s_quadbuf, ivec2(gid), 0).xy;
	else
		quadbufSample = uvec2(-1u, 0u);
	
	// Shade the cluster
	
	uint clusterValue = quadbufSample.x;
	uint clusterSubsamples = quadbufSample.y;
	vec3 clusterResult;
	if (clusterValue != -1u && clusterSubsamples != 0) {
		
		vec2 clusterCentroid = subsamplePosAverage(clusterSubsamples);
		clusterResult = shadeVisSamplePBR(clusterValue, vec2(gid & ~1u) + clusterCentroid);
		imageStore(i_target, ivec2(gid), vec4(clusterResult, 1.0));
		
	}
	
}
